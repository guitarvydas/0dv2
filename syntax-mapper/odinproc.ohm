
OdinProcSignature {
  program = item+
  item =
    | proc -- proc
    | anyToken -- other
    
  proc = procSignature procDefinitionBody

  procDefinitionBody = "{" ws bodyStuff "}" ws

  procSignature =
    | id ws "::" ws pragma? "❲proc❳" ws odinParameterList odinReturnTypeList -- nonvoid
    | id ws "::" ws pragma? "❲proc❳" ws odinParameterList &procDefinitionBody -- void

  odinParameterList =
    | "(" ws ")" ws -- empty
    | "(" ws lastParameter ")" ws -- single
    | "(" ws notLastParameter+ lastParameter ")" ws -- multiple

  odinReturnType =
    | &procDefinitionBody -- void
    | "->" ws odinReturnTypeList -- typed
  odinReturnTypeList =
    |  "(" ws ")" ws -- empty
    |  "(" ws lastParameter ")" ws -- singlebracketed
    |  "(" ws notLastReturnParameter+ lastReturnParameter ")" ws -- multiple
    | ~"(" singleReturnParameter -- single

  notLastParameter = allocatorOrParameterName anythingButComma "," ws ~")"
  lastParameter = allocatorOrParameterName ~anythingButComma anythingButRPar

  notLastReturnParameter = parameterName? anythingButComma "," ws ~")"
  lastReturnParameter = parameterName? ~anythingButComma anythingButRPar
  singleReturnParameter = ~procDefinitionBody anythingButProcDefinitionBody

  pragma = "#❲force_inline❳" ws

  parameterName =
    | id ws &"," -- sharedtype
    | id ws ":" ws -- named
  allocator =  "❲allocator❳" ws ":=" ws
  allocatorOrParameterName = allocator | parameterName

  bodyStuff = skipTo<"}">
  anythingButComma = skipTo<",">
  anythingButRPar = skipTo<")">
  anythingButProcDefinitionBody = skipTo<procDefinitionBody>

      dqb = "“"
    dqe = "”"
  string = dqb (~dqb ~dqe any)* dqe

    sqb = "❛"
    sqe = "❜"
  charconst = sqb (~sqb ~sqe any)* sqe

  id = "❲" (~"❲" ~"❳" any)* "❳"


    intb = "⟨"
    inte = "⟩"
  integer = intb (~intb ~inte any)* inte

    symb = "❲"
    syme = "❳"
  symbol = symb (~symb ~syme any)* syme
  sym<text> = symb text syme
    
    kwb = "‹"
    kwe = "›"
  keyword = kwb (~kwb ~kwe any)* kwe

    mcb = "⟪"
    mce = "⟫"
  multi_char_operator = mcb (~mcb ~mce any)* mce

  indentc = "⇢"
  dedentc = "⇠"

    vb = "«"
    ve = "»"
  verbatim = vb (~vb ~ve any)* ve

  comment = "⎣" (~"⎣" ~"⎦" any)* "⎦"

  anyToken1 = string | charconst | id | integer | keyword | multi_char_operator | indentc | dedentc | verbatim | comment | symbol | integer | any
  anyToken = anyToken1

    uspc = "・"
    unl = "⦚"
    in_nl = unl
    
    spc = " "
    nl = "\n"

    commentspace = "◦"
    commentnl = "¶"

    stringspace = "□"
    stringnl = "‡"
    charspace = "☐"
    charnl = "†"

    wsc = space | spc | nl | uspc | unl | indentc | dedentc | commentspace | commentnl | stringspace | stringnl | charspace | charnl | verbatim
    ws = wsc*

    skipTo<stopBefore> =
    | &stopBefore -- done
    | inner skipTo<stopBefore> -- continue
  inner =
    | wsc -- ws
    | "(" ws inner* ")" -- nestedparens
    | "{" ws inner* "}" -- nestedbraces
    | ~"(" ~")" ~"{" ~"}" anyToken -- bottom
    
}

