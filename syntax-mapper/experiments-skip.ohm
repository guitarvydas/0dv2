OdinProcSignature {
  Main = (OdinReturnType "{}")+
  ProcSignature = id "::" OdinParameterList OdinReturnTypeList?

  OdinParameterList =
    | "(" ")" -- empty
    | "(" LastParameter ")" -- single
    | "(" NotLastParameter+ LastParameter ")" -- multiple

  OdinReturnType = "->" OdinReturnTypeList
  OdinReturnTypeList =
    |  "(" ")" -- empty
    |  "(" LastParameter ")" -- singlebracketed
    |  "(" NotLastReturnParameter+ LastReturnParameter ")" -- multiple
    | ~"(" SingleReturnParameter -- single
    | ~"(" &"{" -- void

  NotLastParameter = (Allocator | ParameterName) AnythingButComma ","
  LastParameter = (Allocator | ParameterName) ~AnythingButComma AnythingButRPar

  NotLastReturnParameter = ParameterName? AnythingButComma ","
  LastReturnParameter = ParameterName? ~AnythingButComma AnythingButRPar
  SingleReturnParameter = ~"{" AnythingButLBrace

  ParameterName =
    | id &"," -- sharedtype
    | id ":" -- named
  Allocator = "allocator" ":="

  AnythingButComma = SkipTo<",">
  AnythingButRPar = SkipTo<")">
  AnythingButLBrace = SkipTo<"{">
  
  SkipTo<stopBefore> =
    | &stopBefore -- done
    | Inner SkipTo<stopBefore> -- continue
  Inner =
    | "(" Inner* ")" -- nested
    | ~"(" ~")" any -- bottom

  id = idFirst idRest*
  idFirst = letter | "_"
  idRest = alnum | "_"
}
